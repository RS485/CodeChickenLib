buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = 'https://files.minecraftforge.net/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven'

group = "codechicken" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "CodeChickenLib"

targetCompatibility = "1.8"
sourceCompatibility = "1.8"

// Define properties file
ext.configFile = file "build.properties"

configFile.withReader {
    // Load config.  It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "${project.config.mod_version}." + (System.getenv("BUILD_NUMBER") ?: "1")
ext.mcforge = "$config.mc_version-$config.forge_version"
println project.mcforge

// Setup the forge minecraft plugin data. Specify the preferred forge/minecraft version here
minecraft {
    mappings channel: 'snapshot', version: config.mappings
    accessTransformer = file('src/main/resources/ccl_at.cfg')
    //replace '${mod_version}', project.config.mod_version
    //makeObfSourceJar = false

    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                logisticspipes {
                    sources = [sourceSets.main]
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                logisticspipes {
                    sources = [sourceSets.main]
                }
            }
        }
    }
}

dependencies {
    minecraft group: 'net.minecraftforge', name: 'forge', version: project.mcforge

    testCompile 'junit:junit:4.12'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mc_version", project.config.mc_version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mc_version': project.config.mc_version
    }
    rename '(.+_at.cfg)', 'META-INF/$1'
}

def commonManifest = {
    attributes 'FMLAT': 'ccl_at.cfg'
    attributes 'Specification-Title': 'CodeChickenLib'
    attributes 'Specification-Vendor': 'TheCBProject'
    attributes 'Specification-Version': '1'
    attributes 'Implementation-Title': project.name
    attributes 'Implementation-Version': "${project.config.mc_version}-${project.version}"
    attributes 'Implementation-Vendor': 'RS485'
    attributes 'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
}

jar {
    classifier = 'universal'
    finalizedBy 'reobfJar'
    manifest commonManifest
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
    manifest commonManifest
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
    manifest commonManifest
}

//task signJar(type: SignJar, dependsOn: reobfJar) {
//    onlyIf { // Skip the task if our secret data isn't available
//        project.hasProperty('keyStore')
//    }
//
//    if (project.hasProperty('keyStore')) {
//        keyStore = project.keyStore // This needs to be a path to the keystore file
//        alias = project.keyStoreAlias
//        storePass = project.keyStorePass
//        keyPass = project.keyStoreKeyPass
//        inputFile = jar.archivePath
//        outputFile = jar.archivePath
//    }
//}

//build.dependsOn signJar

// Tell the artifact system about our extra jars
artifacts {
    archives deobfJar
    archives srcJar
}

// Configure an upload task.
uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty("local_maven")) {
                repository(url: "file://${local_maven}")
                pom {
                    groupId = project.group
                    // Force the maven upload to use the <mcversion>-<version> syntax preferred at files
                    artifactId = project.archivesBaseName
                    project {
                        name project.archivesBaseName
                        packaging 'jar'
                        description 'CodeChickenLib'
                        url 'https://github.com/TheCBProject/CodeChickenLib'

                        scm {
                            url 'https://github.com/TheCBProject/CodeChickenLib'
                            connection 'scm:git:git://github.com/TheCBProject/CodeChickenLib.git'
                            developerConnection 'scm:git:git@github.com:TheCBProject/CodeChickenLib.git'
                        }

                        issueManagement {
                            system 'github'
                            url 'https://github.com/TheCBProject/CodeChickenLib/issues'
                        }

                        licenses {
                            license {
                                name 'GNU Lesser Public License (GPL), Version 2.1'
                                url 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id 'chicken-bones'
                                name 'chicken-bones'
                                roles { role 'developer' }
                            }
                        }
                    }
                }
            }
        }
    }
}
